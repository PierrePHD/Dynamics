AABZ
PR_ProblemeEspace
DEBPROC PR_ProblemeEspace M C K CL_A CHA dt g It_m ;


    Int1 = PR_IntegrProdLine g.u g.u dt ;
    Int2 = PR_IntegrProdLine g.v g.u dt ;
    Int3 = PR_IntegrProdLine g.w g.u dt ;

    Premier = (Int1*K) et (Int2*C) et (Int3*M);

* Contribution des efforts
  NbCharg = DIMENSION CHA ;
  Bool1 = (NbCharg > 0);

  SI Bool1;
      REPETER It NbCharg ;
        CHA_i = EXTR CHA CHAR &It ;
        CHP = EXTR CHA_i CHAM ;
        EVO = EXTR CHA_i EVOL ;
        CH_ord = EXTR EVO ORDO ;
        CH_abs = EXTR EVO ABSC ;
        Int = PR_IntegrProdLine CH_ord g.u CH_abs ;

        Si (&It ega 1) ;
          Second = (CHP*Int) ;
        Sinon ;
          Second = Second + (CHP*Int) ;
        FinSi ;
      FIN It ;
  FINSI;

* La somme sur les modes precedents

    FamiFTot FamiGTot = PR_ConcatenerFami (It_m-1) ;

    TailFami = DIMENSION FamiFTot;
    Bool2 = TailFami > 0;

    Si Bool2;

        Som1 = 0;
        Som2 = 0;
        Som3 = 0;

        REPETER Mod TailFami;
            gk = FamiGTot.&Mod;
            fk = FamiFTot.&Mod;

            g_gk   = PR_IntegrProdLine g.u gk.u dt ;
            g_gkp  = PR_IntegrProdLine g.u gk.v dt ;
            g_gkpp = PR_IntegrProdLine g.u gk.w dt ;

            Som1 = Som1 + (g_gk   * fk);
            Som2 = Som2 + (g_gkp  * fk);
            Som3 = Som3 + (g_gkpp * fk);

        Fin Mod;

        Si Bool1;
            Second = Second - ( (K*Som1) + (C*Som2) + (M*Som3) ) ;
        SiNon;
            Second =  (-1) * ( (K*Som1) + (C*Som2) + (M*Som3) ) ;
        FinSi;

    FinSi;

Si ((NON Bool1) ET (NON Bool2));
    Mess 'il n y a ni effort ni deplacements imposes';
    Mess '--------------------------------------------';
    list CHA.1;
FinSi;

* Resolution

  Premier = Premier ET CL_A;
  f = RESO Premier Second;
  RESPRO f;


FINPROC;
