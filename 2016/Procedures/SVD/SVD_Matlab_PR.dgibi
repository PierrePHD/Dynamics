ABSZ
PR_SVD_Matlab
DEBPROC PR_SVD_Matlab NbModPOD*ENTIER;

    // Une nouvelle procedure POD sera disponible 2016


    Pb.Gpoin = CHAN POI1 (Pb.geo) ; // Changement pour Extraire une liste
    Nb_Noeu = NBNO (Pb.geo) ;

    L_Comp = EXTR (ENLE (Sol_Dir.U.1) 'LX' ) 'COMP' ; // L_f ; // MOTS UX UY UZ RX RY RZ;
    Nb_Comp = DIME L_Comp ;
    L_Out = PROG;

    // Creation du fichier pour lecture exterieure
        REPETER It_T En_Nb_Re ;
            REPETER It_Comp Nb_Comp ;
                Comp_it = (EXTR L_Comp &It_Comp) ;
                CP_U = EXCO Comp_it (Sol_Dir.U.&It_T) Comp_it ; // Pour n'avoir qu'une seule composante (sinon EXTR ne fonctionne pas)
                L_Out = L_Out ET (EXTR CP_U 'VALE' (Pb.Gpoin)) ;
            Fin It_Comp ;
        Fin It_T ;

        opti sort 'FileOUT' ;
        sort 'EXCE' L_Out 'SEPA' 'ESPA';
            Com_Unix = CHAINE './sc_ext.sh ' NbModPOD ' ' Nb_Comp ' ' Nb_Noeu ;
            EXTE ( Com_Unix ) ; // Exploitation par Matlab
        Del_File = CHAINE 'rm FileOUT.csv' ;
        EXTE ( Del_File ) ;

    // Lecture des fichiers generes par Matlab

        // Valeurs singulieres
            OPTI 'ACQU' 'FileMatValS' ;
            ACQUERIR   Lis_ValS*LISTREEL NbModPOD ;
                Del_File = CHAINE 'rm FileMatValS' ;
                EXTE ( Del_File ) ;

        // Vecteurs singuliers
            Tab_Mod = TABLE ;
            // Bas_Mob = SUITE ;
            REPETER It_Mod NbModPOD ;
                REPETER It_Comp Nb_Comp ;
                    Comp_it = (EXTR L_Comp &It_Comp) ;
                    NomFile = CHAINE
                    'FileMatlab_' (PR_FormatNombre &It_Mod 3) '_Comp_' (PR_FormatNombre &It_Comp 3) ;
                    OPTI 'ACQU' NomFile ;
                    ACQUERIR   Lis_01*LISTREEL Nb_Noeu ;
                        Del_File = CHAINE 'rm ' NomFile ;
                        EXTE ( Del_File ) ;

                    Si (&It_Comp ega 1) ;
                        Mod_comp = MANU 'CHPO' (Pb.Gpoin) Comp_it Lis_01 ;
                    SiNon ;
                        Mod_comp = Mod_comp + (MANU 'CHPO' (Pb.Gpoin) Comp_it Lis_01) ;
                    FinSi ;
                Fin It_Comp ;
                Tab_Mod.&It_Mod = Mod_comp ;
                // Bas_Mob = Bas_Mob ET Mod_comp ;

            Fin It_Mod ;

    RESPRO Lis_ValS;
    RESPRO Tab_Mod;

FINPROC;
