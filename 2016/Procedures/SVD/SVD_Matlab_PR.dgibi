ABMZ
PR_SVD_Matlab
DEBPROC PR_SVD_Matlab ;



    // Nouvelle procedure 2016
    // Lis_CHP = SUITE;
    //
    // REPETER It_T En_Nb_Re ;
    //     Lis_CHP = Lis_CHP et (Solu1.U.&It_T) ;
    // Fin It_T ;
    //
    // L_POD L_ValS = POD Lis_CHP 'CLASSIQUE' NbModPOD ;

    Pb.Gpoin = CHAN POI1 (Pb.geo) ; // Changement pour EXTRaire une liste
    Nb_Noeu = NBNO (Pb.geo) ;

    L_Comp = EXTR (ENLE (Solu1.U.1) 'LX' ) 'COMP' ; // L_f ; // MOTS UX UY UZ RX RY RZ; 
    list L_Comp ;
    Nb_Comp = DIME L_Comp ;
    L_Out = PROG;

    // Creation du fichier pour lecture exterieure
        REPETER It_T En_Nb_Re ;
            REPETER It_Comp Nb_Comp ;
                Comp_it = (EXTR L_Comp &It_Comp) ;
                CP_U = EXCO Comp_it (Solu1.U.&It_T) Comp_it ; // Pour n'avoir qu'une seule composante (sinon EXTR ne fonctionne pas)
                L_Out = L_Out ET (EXTR CP_U 'VALE' (Pb.Gpoin)) ;
            Fin It_Comp ;
        Fin It_T ;

        opti sort 'FileOUT' ;
        sort 'EXCE' L_Out 'SEPA' 'ESPA';
            Com_Unix = CHAINE './sc_ext.sh ' NbModPOD ' ' Nb_Comp ' ' Nb_Noeu ;
            EXTE ( Com_Unix ) ; // Exploitation par Matlab
        Del_File = CHAINE 'rm FileOUT.csv' ;
        EXTE ( Del_File ) ;

    // Lecture des fichiers generes par Matlab

        // Valeurs singulieres
            OPTI 'ACQU' 'FileMatValS' ;
            ACQUERIR   Lis_ValS*LISTREEL NbModPOD ;
                Del_File = CHAINE 'rm FileMatValS' ;
                EXTE ( Del_File ) ;

        // Vecteurs singuliers
            Tab_Mod = TABLE ;
            REPETER It_Mod NbModPOD ;
                REPETER It_Comp Nb_Comp ;
                    Comp_it = (EXTR L_Comp &It_Comp) ;
                    NomFile = CHAINE 'FileMatlab_' (PR_FormatNombre &It_Mod) '_Comp_' (PR_FormatNombre &It_Comp) ;
                    OPTI 'ACQU' NomFile ;
                    ACQUERIR   Lis_01*LISTREEL Nb_Noeu ;
                        Del_File = CHAINE 'rm ' NomFile ;
                        EXTE ( Del_File ) ;

                    Si (&It_Comp ega 1) ;
                        Tab_Mod.&It_Mod = MANU 'CHPO' (Pb.Gpoin) Comp_it Lis_01 ;
                    SiNon ;
                        Tab_Mod.&It_Mod = Tab_Mod.&It_Mod
                        + (MANU 'CHPO' (Pb.Gpoin) Comp_it Lis_01) ;
                    FinSi ;
                Fin It_Comp ;
            Fin It_Mod ;

    RESPRO Lis_ValS;
    RESPRO Tab_Mod;

FINPROC;
