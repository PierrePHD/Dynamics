AAPZ
PR_CalculErreur
DEBPROC PR_CalculErreur Coord/MOT BOOL2/LOGIQUE;

BOOL1 = EXISTE Coord;
BOOL3 = OU (NON BOOL1) (EXISTE BOOL2);

    ErrDepMo = PROG;
    ErrParMo = PROG;
    EneTot = PROG;

    REPETER It_m NbModCal;
        rec_sol = PR_PGD_to_Full &It_m;
        Err = PR_Difference_Solu Solu1.u rec_sol.u;
        ErrMax = PROG;
        ErrTotM = -0.1;
        REPE IT_T En_Nb_Re;
            in_T = &IT_T - 1;
            SI BOOL1;
                ErrTotM = MAXI ( PROG
                                    (MAXI (ABS ((EXCO Coord Err.in_T))))
                                    ErrTotM
                                );
                SoluT = EXCO Coord Solu1.u.in_T;
                Si (&IT_T ega 1) ;
                    Max_Dep = MAXI SoluT;
                    Min_Dep = MINI SoluT;
                SINON;
                    Max_Dep = MAXI ( PROG (MAXI SoluT) Max_Dep );
                    Min_Dep = MINI ( PROG (MINI SoluT) Min_Dep );
                FINSI;
            FINSI;
            SI BOOL3;
                ErrMaxT = XTMX Err.in_T K;
                ErrMax = ErrMax et ErrMaxT;
                Si (&It_m ega 1) ;
                    EneTotT = XTMX Solu1.u.in_T K;
                    EneTot = EneTot et EneTotT;
                FINSI;
            FINSI;
        FIN IT_T;
        SI BOOL1;
            ErrDepMo = ErrDepMo ET ErrTotM;
        FINSI;
        SI BOOL3;
            ErrTotEV = EVOL MANU T Vec_T Errr ErrMax ;
            EneTotEV = EVOL MANU T Vec_T Errr EneTot ;
            ErrParMo = ErrParMo ET (SOMM ErrTotEV);
        FINSI;
    FIN It_m;


    SI (NbModCal ega 1);
        LMod = PROG 1;
    SINON;
        LMod = (PROG 1 PAS 1 NbModCal);
    FINSI;

    SI BOOL1;
        Ampli = Max_Dep - Min_Dep;
        Mess 'Max_Dep' Max_Dep 'Min_Dep' Min_Dep 'Ampli' Ampli;
        ErrDepMo = ErrDepMo / Ampli;
        Mess 'Erreur Relative en Deplacement sur ' Coord
            ' en fonction du nombre de mode';
        list (EVOL MANU Mode LMod Erreur ErrDepMo);

        RESPRO ErrDepMo;
    FINSI;
    SI BOOL3;
        ErrParMo = ErrParMo / (EXTR (SOMM EneTotEV) 1) ;
        Mess 'Erreur Energetique en fonction du nombre de mode';
        list (EVOL MANU Mode LMod Erreur ErrParMo);

        RESPRO ErrParMo;
    FINSI;

FINPROC;
