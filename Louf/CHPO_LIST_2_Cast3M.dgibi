OPTI DIME 2 ;
OPTI ELEM QUA4 ; 
* 
P1 = 0. 0. ;
P2 = 1. 0. ;
P3 = 1. 1. ; 
P4 = 0. 1. ; 
* 
L12 = DROI 4 P1 P2 ;
L23 = DROI 4 P2 P3 ;
L34 = DROI 4 P3 P4 ;
L41 = DROI 4 P4 P1 ;
* 
MAI1 = DALL L12 L23 L34 L41 ; 
* 
TRAC MAI1 ; 
* 
* Un champ par point parmi d'autres.... 
* 
U1 = (((COOR 1 MAI1)**2) +  ((COOR 2 MAI1)**2))**0.5  ; 
* 
* On trace le champ U1
* 
TRAC U1 MAI1 ; 
* 
* On transforme à la main le maillage global en SEG2 
* Le maillage sera donc une ligne et on pourra construire 
* une évolution dont on pourra extraire l'ordonnee, sous forme d'une liste 
* -> pas terrible, mais à faire une seule fois ! 
* 
MAI2 = CHAN POI1 MAI1 ; 
OPTI ELEM SEG2 ;
PI1 = MAI2 POIN 1 ;
N1N = NBNO MAI1 ; 
REPE BCL1N (N1N-1) ;  
  PI2 = MAI2 POIN (&BCL1N+1) ;
  SI (&BCL1N EGA 1) ; 
    MAI3 = DROI 1 PI1 PI2 ; 
    list MAI3 ; 
  SINON ; 
    MAI3 = MAI3 ET (DROI 1 PI1 PI2) ; 
  FINSI ; 
  PI1 = PI2 ; 
FIN BCL1N ;
* 
* Le maillage ne ressemble à rien, mais c'est normal 
* 
TRAC MAI3 ; 
* 
* LIS1 = PROG ; 
* 
* REPE BCL1N (NBNO MAI1) ; 
* 
* PI1 = MAI2 POIN &BCL1N ; 
* LIS1 = LIS1 ET (PROG (EXTR U1 'SCAL' PI1)) ; 
* 
* FIN BCL1N ; 
* 
* Ce qu'il faudra faire à chaque CHPO (chaque pas de temps)
* (pour une composante précise : EVOL 'CHPO' U1 'NOM COMPOSANTE' MAI3)
EVO1 = EVOL 'CHPO' U1 MAI3 ; 
LIS1 = EXTR EVO1 'ABSC' ; 
* 
* On retransforme la liste en champ par point 
*  
LIS2 = LIS1 ; 
U2 = MANU 'CHPO' MAI1 'SCAL' LIS2 ;  
* 
* On trace le champ U2 pour vérifier qu'il est pareil que U1 
* 
TRAC U2 MAI1 ; 