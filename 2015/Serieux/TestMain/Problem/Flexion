    OPTI DIME 3 ELEM SEG2 MODE TRID;
    Pb = TABLE;
    V0001 = TABLE;
    *---------------------DONNEES---------------------*
    *Unites SI
    AMP1 = -100.0 ;
    LON1 = 0.5 ;
    LON2 = -0.1 ;
    SEC1 = 2.e-4 ;
    IY1 = (8/12.)*1e-8 ;
    IZ1 = (2/12.)*1e-8 ;
    NUP = 0.3 ;
    YOP = 2.1E11 ;
    YOP2 = YOP/((1.25e4)) ;
    RHOP = 7800.0 ;
    *--------------------GEOMETRIE--------------------*
    P1 = 0.   0. 0. ;
    Pb.P0 = P1 ;
    P2 = LON1 0. 0. ;
    Pb.PS = P2 ;
    L1 = D 20 P1 P2 ;
    P3 = L1 POIN PROC ((LON1/2) 0. 0.);
    P4 = LON1 0. LON2 ;
    ELIM L1 0.00000001;
    L2 = D 1 P2 P4;
    Pb.geo = L1 et L2;
    *opti trac X ;
    TRAC Pb.geo ;
    *------DEFINITION DU MODELE ET DU MATERIAU--------*
    MOD1 = MODEL L1 MECANIQUE ELASTIQUE POUT ;
    MOD2 = MODEL L2 MECANIQUE ELASTIQUE POUT ;
    MODTOT = MOD1 et MOD2 ;
    Pb.Mob = MODTOT;
    MAT1 = MATER MOD1 YOUN YOP NU NUP RHO RHOP ;
    MAT2 = MATER MOD2 YOUN (YOP2/2.) NU NUP RHO (RHOP/100.) ;
    CAR1 = CARAC MOD1 SECT SEC1 INRY IY1 INRZ IZ1
           TORS 1. VECT (0. 1. 0.) ;
    CAR2 = CARAC MOD2 SECT SEC1 INRY IY1 INRZ IZ1
           TORS 1. VECT (0. 1. 0.) ;
    MAT1B = MAT1 ET CAR1 ;
    MAT2B = MAT2 ET CAR2 ;
    MATTOT = MAT1B et MAT2B ;
    *-------MATRICES DE RIGIDITE ET DE MASSE----------*
    Pb.K = RIGI MODTOT MATTOT ;
    Pb.M = MASS MODTOT MATTOT ;

    *--------------CONDITIONS LIMITES-----------------*
    CL1 = BLOQ P1 UY UX RZ UZ RX RY ;
    CL2 = BLOQ P4 UY UX RZ UZ RX RY ;
    *CL2 = BLOQ L1 UZ RX RY ;
    CL_T = CL1 ET CL2 ;
    Pb.CL_TOT= CL_T;
    * ET CL2 ;

    *--------------Parametres de calcul---------------*
    V0001.dt = 2e-5;
    V0001.Ttot = 10e-3;
    Pb.T_period = 5e-3 ;
    En_Nb_Re = PR_TailVectTemp V0001.dt V0001.Ttot;
    Vec_T = PROG 0. PAS V0001.dt V0001.Ttot ;

    *------------EVOLUTION TEMPORELLE-----------------*
    LIS1 = (PROG sinu (1/Pb.T_period) PHAS 270 Vec_T) ;
    LIS1 = LIS1 + (PROG En_Nb_Re*1.);
    EVT1 = EVOL MANU T Vec_T G(T) LIS1 ;
    EVT2 = EVOL MANU T Vec_T G(T) (Vec_T*2) ;
    *DESS EVT1;
    *-----------------CHARGEMENT----------------------*
    FOR1 = FORCE FZ (AMP1*2.) P2 ;
    FOR2 = FORCE FX 0. P3 ;
    CHA1 = CHAR FORC FOR1 EVT1 ;
    CHA2 = CHAR FORC FOR2 EVT2 ;
    Pb.CHA_TOT = CHA1 et CHA2 ;

    dep0 = MANU CHPO L1 3 'UX' 0. 'UY' 0. 'UZ' 0. NATURE 'DIFF';
    Pb.U0  = dep0;
    Pb.V0  = dep0;

    Pb.P_intere = P2;
